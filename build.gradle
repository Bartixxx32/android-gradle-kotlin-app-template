buildscript {
    rootProject.ext.ci = rootProject.hasProperty("ci")

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle:build-scan-plugin:1.7.2"
        classpath "com.android.tools.build:gradle:2.3.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.2-3" // Breaks with 1.1.2-4
        classpath "com.novoda:gradle-android-command-plugin:1.7.1"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.4"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.3.0"
        classpath "com.github.triplet.gradle:play-publisher:1.2.0"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.10"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.15.0"
    }
}

repositories {
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
    distributionType = Wrapper.DistributionType.ALL
}

apply from: rootProject.file("gradle/buildScan.gradle")
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "android-command"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "burrows.apps.example.template"
        versionCode 1
        versionName "1.0"
        minSdkVersion 16
        targetSdkVersion 25
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en" // Keep only english resource files for now
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries !rootProject.ext.ci
    }

    // Shouldn"t the plugin be doing this for us?
    sourceSets {
        androidTest.java.srcDirs += "src/androidTest/kotlin"
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        textOutput "stdout"
        checkAllWarnings true
        warningsAsErrors true
        disable "UnusedResources"              // Unused will be removed on release
        disable "IconExpectedSize"             // Using the material icons provided from Google
        disable "GoogleAppIndexingApiWarning"  // We might want to index our app later
        disable "InvalidPackage"               // Butterknife, Okio and Realm
        disable "ResourceType"                 // Annotation binding
        disable "GradleDependency"
        disable "NewerVersionAvailable"
    }

    testOptions {
        animationsDisabled = true

        unitTests {
            returnDefaultValues = true

            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }

    packagingOptions {
        exclude "**/*.txt"
        exclude "**/*.xml"
        exclude "**/*.properties"
    }
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:25.3.1"
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.2-4"

    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:25.3.1"
    compile "com.google.dagger:dagger:2.11"

    kapt "com.google.dagger:dagger-compiler:2.11"
    kapt "com.android.databinding:compiler:2.3.2"

    // Espresso Instrumentation Testing
    androidTestCompile "com.android.support:support-annotations:25.3.1"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }

    // Junit
    testCompile "junit:junit:4.12"

    // Mockito
    testCompile "org.mockito:mockito-core:2.8.9"

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-support-v4:3.2.2"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"

    // AssertJ
    testCompile "com.android.support:support-annotations:25.3.1"
    testCompile "org.assertj:assertj-core:1.7.1"
}
