buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
    }

    dependencies {
        // Gradle build receipt
        classpath "com.gradle:build-receipt-plugin:1.2"
        // Android gradle plugin
        classpath "com.android.tools.build:gradle:2.1.2"
        // Android command plugin
        classpath "com.novoda:gradle-android-command-plugin:1.5.0"
        // Google error-prone
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
        // Publish to google play store
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        // Dex method count
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.4"
        // Check for plugin updates
        classpath "com.github.ben-manes:gradle-versions-plugin:0.12.0"
        // Apk size
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.2.0"
        // Code coverage
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
        // kotlin plugin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.2"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:1.0.2"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
}

apply from: rootProject.file("gradle/receipt.gradle")
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "android-command"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.triplet.play"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.github.kt3k.coveralls"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.vanniktech.android.apk.size"
apply from: rootProject.file("./gradle/compile.gradle")
apply from: rootProject.file("./gradle/android.gradle")
apply from: rootProject.file("./gradle/codeQuality.gradle")

android {
    defaultConfig {
        applicationId "burrows.apps.example.template"
        versionCode 1
        versionName "1.0"
        // For espresso tests
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // Shouldn"t the plugin be doing this for us?
    sourceSets {
        androidTest.java.srcDirs += "src/androidTest/kotlin"
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro")
            signingConfig signingConfigs.debug
        }
    }
}

// Publishing to google play
play {
    serviceAccountEmail = "jaredsburrows@gmail.com"
    pk12File = rootProject.file("./.buildscript/key.p12")
    track = "production" // or "beta" or "alpha"
}

// Only allows specifying of a single file
coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoDebugReport/jacocoDebugReport.xml"
}

// https://github.com/tbroyer/gradle-errorprone-plugin/issues/18
configurations.all {
    resolutionStrategy {
        force "com.google.errorprone:error_prone_core:2.0.5"
    }
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.2"

    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:24.0.0"

    // Espresso Instrumentation Testing
    androidTestCompile "com.android.support:support-annotations:24.0.0"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: "appcompat-v7"
        exclude module: "support-v4"
        exclude module: "recyclerview-v7"
    }

    // Unit Tests
    // AssertJ
    testCompile("com.squareup.assertj:assertj-android:1.1.1") { // 2/3.1.0 for java 8
        exclude module: "support-v4"
        exclude module: "support-annotations"
    }

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-play-services:3.1"
    testCompile "org.robolectric:shadows-support-v4:3.1"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"
}
